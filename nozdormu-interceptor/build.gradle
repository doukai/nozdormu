plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

group 'org.graphoenix'
version '0.1.0'

repositories {
    mavenCentral()
    jcenter()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':nozdormu-common')
    implementation project(':nozdormu-inject')
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.25.3'
    implementation 'com.google.auto.service:auto-service:1.0.1'

    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testAnnotationProcessor project(':nozdormu-inject')
    testAnnotationProcessor project(':nozdormu-interceptor')
    testAnnotationProcessor 'com.google.auto.service:auto-service:1.0.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.graphoenix'
            artifactId = 'nozdormu-interceptor'

            from components.java

            pom {
                name = 'nozdormu-interceptor'
                description = 'nozdormu-interceptor'
                url = 'https://github.com/doukai/nozdormu'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doukai'
                        name = 'Dou Kai'
                    }
                }
                scm {
                    url = 'https://github.com/doukai/nozdormu'
                    connection = 'scm:git://github.com:doukai/nozdormu.git'
                    developerConnection = 'scm:git://github.com:doukai/nozdormu.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

tasks.build.doFirst {
    mkdir "$buildDir/jreleaser"
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}