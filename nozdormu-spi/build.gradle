plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

group 'org.graphoenix'
version '0.1.0'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation 'com.google.auto.service:auto-service:1.0.1'

    api 'io.projectreactor:reactor-core:3.4.16'
    api 'jakarta.annotation:jakarta.annotation-api:2.0.0'
    api 'jakarta.inject:jakarta.inject-api:2.0.1'
    api 'jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.1'
    api 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    api 'org.eclipse.microprofile.config:microprofile-config-api:3.0.1'
    api 'org.tinylog:tinylog-api:2.4.1'

    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.graphoenix'
            artifactId = 'nozdormu-spi'

            from components.java

            pom {
                name = 'nozdormu-spi'
                description = 'nozdormu-spi'
                url = 'https://github.com/doukai/nozdormu'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doukai'
                        name = 'Dou Kai'
                    }
                }
                scm {
                    url = 'https://github.com/doukai/nozdormu'
                    connection = 'scm:git://github.com:doukai/nozdormu.git'
                    developerConnection = 'scm:git://github.com:doukai/nozdormu.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

tasks.build.doFirst {
    mkdir "$buildDir/jreleaser"
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}